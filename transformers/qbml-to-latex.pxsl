<<<?xml version="1.0" encoding="UTF-8" ?>>>

,tag source result =
  template <(,source)>
    text
      <<\>>
      ,result
      <<{>>
    apply-templates
    text <<}>>

,line body =
    text
      ,body
      <<&#xa;>>

,env name =
    ,line <(
      <<\begin{>>
      ,name
      <<}>> )>
    ,BODY
    ,line <(
      <<\end{>>
      ,name
      <<}>> )>

,enva name el =
  ,env <(,name)>
    apply-templates <(,el)>

,renew cmd attr =
    text
      <<\renewcommand{\>>
      ,cmd
      <<}{>>
    apply-templates <(,attr)>
    ,line }


stylesheet -version=1.0 -xmlns:xsl=http://www.w3.org/1999/XSL/Transform
  output -method=text -encoding=UTF-8
  preserve-space *

  template qpdb
    ,line <(<<% !TEX TS-program = XeLaTeX>>)>
    ,line <(<<\documentclass[10pt]{packet}>>)>
    ,line <(<<\input{\jobname.edges}>>)>
    ,enva document tournaments

  template tournament
    ,renew tournamentname @name
    ,renew tournamentyear @year
    ,renew subtitle subtitle
    apply-templates packets

  template packet
    ,renew packetname @name
    ,renew authors @authors
    ,line <(<<\maketitle>>)>
    apply-templates tossups
    ,line <(<<\clearpage>>)>
    apply-templates boni

  template tossups
    ,enva tossups tossup

  template tossup
    ,env question
      apply-templates question
      ,line <(<<\\*>>)>
      apply-templates answer
      ,line <()>

  template boni
    ,enva bonuses bonus

  template bonus
    ,env question
      apply-templates stem
      apply-templates part

  template part
    ,line <(<<\\*[1mm]>>)>
    text <<\partvalue{>>
    value-of @value
    text <<} >>
    apply-templates question
    ,line <(<<\\*>>)>
    apply-templates answer
    ,line <()>

  template question
    apply-templates


  ,tag answer answer
  ,tag answer//req req
  ,tag req power
  ,tag title textit
  ,tag sup textsuperscript
  ,tag sub textsubscript
  ,tag sc textsc

  template pron
    text <<\pron{>>
    apply-templates graph
    text <<}{>>
    apply-templates phon
    text <<}>>

  template br <<\\>>

  template w
    text <<\w{>>
    apply-templates
    text <<}{>>
    value-of @i
    text <<}>>

