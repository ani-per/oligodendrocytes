<<<?xml version="1.0" encoding="UTF-8" ?>>>

,tag source result =
  template <(,source)>
    text <<\>>
    text
      ,result
    text <<{>>
    apply-templates
    text <<}>>

stylesheet -version=1.0 -xmlns:xml=http://www.w3.org/XML/1998/namespace -xmlns:xsl=http://www.w3.org/1999/XSL/Transform
  output -method=text -encoding=UTF-8
  preserve-space -elements=*

  template qpdb
    text <<% !TEX TS-program = XeLaTeX
\documentclass[10pt]{packet}

\input{\jobname.edges}

\begin{document}
>>
    apply-templates tournaments
    text <<
\end{document}
>>

  template tournament
    ,renew cmd attr =
      text <<	\renewcommand{\>>
      text
        ,cmd
      text <<}{>>
      apply-templates <(,attr)>
      text <<}
>>

    text <<
>>
    ,renew tournamentname @name
    ,renew tournamentyear @year
    ,renew subtitle subtitle
    text <<	>>

    apply-templates packets

  template packet
    text <<
	\newcommand{\packetname}{>>
    value-of @name
    text <<}
	\newcommand{\authors}{>>
    value-of @authors
    text <<}

	\maketitle

	>>
    apply-templates tossups
    text <<

	\clearpage

	>>
    apply-templates boni

  template tossups
    text <<
	\subsection*{Tossups}
	>>
    apply-templates tossup

  template tossup
    text <<
	\begin{question}%
		>>
    apply-templates question
    text <<\\
		>>
    apply-templates answer
    text <<
	\end{question}
>>

  template boni
    text <<
	\subsection*{Bonuses}
	\begin{enumerate}[leftmargin=0pt]
	>>
    apply-templates bonus
    text <<
	\end{enumerate}
>>

  template bonus
    text <<
	\item\hyperdef{bonus}{\arabic{enumi}}{}
	\begin{minipage}[t]{\linewidth}
		>>
    apply-templates stem
    apply-templates part
    text <<
	\end{minipage}

>>

  template part
    text <<\\[1mm]
		\partvalue{>>
    value-of @value
    text <<} >>
    apply-templates question
    text <<\\
		>>
    apply-templates answer

  template question
    apply-templates


  ,tag answer answer
  ,tag req req
  ,tag question//req power
  ,tag title textit
  ,tag sup textsuperscript
  ,tag sub textsubscript
  ,tag sc textsc

  template w
    text <<\w{>>
    apply-templates
    text <<}{>>
    value-of @i
    text <<}>>

